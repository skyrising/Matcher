plugins {
    id 'java'
	id 'application'
	id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
	id 'org.openjfx.javafxplugin' version '0.0.10'
	id 'de.jjohannes.extra-java-module-info' version '0.5'
}

repositories {
    mavenCentral()
    maven {
        name "Fabric"
        url 'https://maven.fabricmc.net/'
    }
}

archivesBaseName = 'matcher'
group = 'net.fabricmc'

sourceSets {
    main {
        java.srcDirs = ['src']
		resources.srcDirs = ['res']
    }
}

javafx {
	version = "11.0.2"
	modules = [ 'javafx.controls', 'javafx.web' ]
}

java {
	sourceCompatibility = JavaVersion.VERSION_11
	targetCompatibility = JavaVersion.VERSION_11

	if (!JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_11)) {
		toolchain {
			languageVersion = JavaLanguageVersion.of(11)
		}
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 11
}

dependencies {
    implementation "com.github.javaparser:javaparser-core:3.8.3"
	implementation "org.bitbucket.mstrobel:procyon-compilertools:0.5.33.6"
	implementation "org.ow2.asm:asm:9.1"
	implementation "org.ow2.asm:asm-commons:9.1"
	implementation "org.ow2.asm:asm-tree:9.1"
	implementation "org.ow2.asm:asm-util:9.1"
	implementation "org.benf:cfr:0.151"
	implementation "net.fabricmc:fabric-fernflower:1.3.0"
	implementation "org.eclipse.jgit:org.eclipse.jgit:5.11.1.202105131744-r"
}

extraJavaModuleInfo {
	automaticModule("cfr-0.151.jar", "cfr")
	automaticModule("procyon-compilertools-0.5.33.6.jar", "procyon.compilertools")
	automaticModule("procyon-core-0.5.33.6.jar", "procyon.core")
	automaticModule("fabric-fernflower-1.3.0.jar", "intellij.fernflower")
	automaticModule("JavaEWAH-1.1.7.jar", "javaewah")
}

application {
	mainModule = 'matcher'
	mainClass = 'matcher.Main'
}

build.dependsOn(shadowJar)

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact shadowJar
		}
	}
}